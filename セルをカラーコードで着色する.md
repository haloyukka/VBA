
```
Sub セルのカラーコードで着色()
    Dim i As Integer
    Dim val As String
    Dim r As Integer, g As Integer, b As Integer

    For i = 1 To Selection.Count
        'セルに書かれたカラーコードを取得
        val = Replace(Selection.Cells(i).Value, "#", "")
        If (Len(val) = 6) Then
            'RGB各色を整数に変換
            r = Hex2Dec(Mid(val, 1, 2))
            g = Hex2Dec(Mid(val, 3, 2))
            b = Hex2Dec(Mid(val, 5, 2))
            '背景色が暗い場合は文字を白に変える
            If ((r + g + b) < 512) Then
                Selection.Cells(i).Font.ColorIndex = 2
            Else
                Selection.Cells(i).Font.ColorIndex = 1
            End If
            'セルの塗り色を変更
            Selection.Cells(i).Interior.Color = RGB(r, g, b)
        End If
    Next

End Sub

'2文字の16進数文字列（FF, 8Aなど）を整数に変換
Private Function Hex2Dec(Value As String) As Integer
    Hex2Dec = CInt("&H" & Value)
End Function
```
